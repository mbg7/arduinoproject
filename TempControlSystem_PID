/* ***********************************************************************
This file contains the PID Control function.  PID coefficients were 
provided initially by Dr. Palmeri.  They may be improved in the future
using the AutoTune library.
************************************************************************ */

void PIDHeaterControl(){
  double currentVal = analogRead(readTempPin);  // Reading from the thermocouple pin (vals are on a range from 0 to 1023)
  double currentVolt = (currentVal/1023)*5;    // Map that reading to a voltage (map 0 to 0V and 1023 to 5V)
  currentTemp = currentVolt/(.005);           // Map that voltage to a temperature (5 mV / Degree C)
  Serial.println(currentTemp);
 
 // CHECK OVERTEMP CONDITION
  if(currentTemp>=maxTemp){   // If we have exeeded the max temp limit, alert user and turn off oven.
      lcd.clear();
      lcd.print("WARNING: TOO HOT!");
      lcd.setCursor(0,1);
      lcd.print("DISCONNECT PWR"); 
      Serial.print("OVERHEAT TEMP: "); 
      Serial.println(currentTemp);
      digitalWrite(heaterPin, LOW); // Turn off the heater
      OverTempAlarm = true;  // Enter the OverTempAlarm state --> this locks up the controller until the user resets
  }
  
  if(!OverTempAlarm){
      display_currentTemp();
      myPID.Compute();  // Takes in the k coefficients, currentTemp, setTemp, and magically determines Output, 
                        //     a measure (default range of 0 to 255) of "how hard we want to drive the heater."
      analogWrite(heaterPin, Output);  // Using PWM. Default Output range is 0 - 255
      Serial.print("OUTPUT Duty Cycle: "); Serial.println(Output);
  }
}
